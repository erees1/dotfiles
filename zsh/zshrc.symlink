#! /usr/bin/env zsh
SRC_DIR=$(realpath $(dirname $(realpath ${(%):-%x})))
DOT_DIR=$SRC_DIR/..

. $DOT_DIR/utils/funcs.sh

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  . ~/.localrc
fi

## all of our zsh files that we want to source
typeset -U config_files
config_files=($DOT_DIR/**/*.zsh)

# load any env files first so that they can be used in other files
# also defines stuff like $ZSH
for file in ${(M)config_files:#*env.zsh}; do . $file; done

# plugins that have been installed in zsh/install.sh
. $ZSH_PLUGINS_DIR/powerlevel10k/powerlevel10k.zsh-theme
. $ZSH_PLUGINS_DIR/zsh-autosuggestions/zsh-autosuggestions.plugin.zsh
. $ZSH_PLUGINS_DIR/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh
. $ZSH_PLUGINS_DIR/zsh-history-substring-search/zsh-history-substring-search.plugin.zsh
add_to_fpath $ZSH_PLUGINS_DIR/zsh-completions/src

add_to_fpath $ZSH_DOT_DIR/completions

# Activate vim mode. This needs to happen before loading any other keybinding.zsh files 
# so we can override them if we want
bindkey -v

# load everything but env files
for file in ${${config_files:#*env.zsh}}; do . $file; done


[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
